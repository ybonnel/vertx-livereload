plugins {
    id "com.jfrog.bintray" version "1.7.3"
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    jcenter()
}

project.rootProject.file('local.properties').withReader {
    Properties props = new Properties()
    props.load(it)
    project.ext.bintrayUser = props.getProperty("bintray.user")
    project.ext.bintrayApikey = props.getProperty("bintray.apikey")
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            groupId 'fr.ybonnel'
            artifactId 'vertx-livereload'
            version '1.0'
        }
    }
}

bintray {
    user = "$bintrayUser"
    key = "$bintrayApikey"
    pkg {
        repo = 'maven'
        name = 'vertx-livereload'
        userOrg = 'ybonnel'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/ybonnel/vertx-livereload'
        labels = ['vertx', 'livereload']
        publicDownloadNumbers = true
        publications = ['MyPublication']

        version {
            name = '1.0'
            desc = 'Vertx livereload version 1.0'
            vcsTag = '1.0'
        }
    }
}


dependencies {
    compile "io.vertx:vertx-core:3.5.1"
    compile "io.methvin:directory-watcher:0.5.0"
}
